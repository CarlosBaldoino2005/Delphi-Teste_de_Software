unit DAO.NotaFiscal.Test;

interface

uses
  System.SysUtils,
  Model.DAO.Interfaces,
  Model.DAO.NotaFiscal,
  Model.Entities.NotaFiscal,
  Components.Connections.Interfaces,
  Components.Connections.Firedac,
  DUnitX.TestFramework;

type
  [TestFixture]
  TFormacaoERPTest = class
  private
    FConnection: iComponentsConnections;
    FDAO: iModelDAOEntity<TModelEntitiesNotaFiscal>;

  public
    [Setup]
    procedure Setup;

    [TearDown]
    procedure TearDown;

    constructor create;

    [Test]
    procedure ConsultaNotaFiscalPeloNumero;

    [Test]
    procedure ConsultaPeloId;

    [Test]
    procedure Insert;

    [Test]
    procedure Update;

    [Test]
    procedure Delete;

    [Test]
    procedure TestInsertSemCNPJ;
  end;

implementation

procedure TFormacaoERPTest.ConsultaNotaFiscalPeloNumero;
begin
  FDAO.Get('Numero', '1');

  Assert.AreEqual('06968708000117', FDAO.This.CNPJEmitente);
end;

procedure TFormacaoERPTest.ConsultaPeloId;
begin
  FDAO.Get('40D3E554-99A1-4001-83DF-D7E8BBB85405');

  Assert.AreEqual('1', FDAO.This.Numero);
  Assert.AreEqual('06968708000117', FDAO.This.CNPJEmitente);
  Assert.AreEqual('42733782000118', FDAO.This.CNPJDestinatario);
  Assert.AreEqual('100', FDAO.This.ValorTotal.ToString);
end;

constructor TFormacaoERPTest.create;
begin
  FConnection := TComponentsConnectionsFiredac.New;
end;

procedure TFormacaoERPTest.Delete;
var
  id: string;
begin
  FDAO.This
    .Numero('101')
    .ChaveEletronica('1234')
    .CNPJEmitente('89743943000118')
    .CNPJDestinatario('89718997000123')
    .ValorTotal(100);

  id := FDAO.Insert.This.Id;

  FDAO.Delete;

  FDAO.Get(id);
  Assert.IsEmpty(FDAO.This.Id);
end;

procedure TFormacaoERPTest.Insert;
begin
  FDAO.This
    .Id('')
    .Numero('101')
    .ChaveEletronica('1234')
    .CNPJEmitente('89743943000118')
    .CNPJDestinatario('89718997000123')
    .ValorTotal(100);

  Assert.WillNotRaise(
    procedure
    begin
      FDAO.Insert;
    end);

  Assert.IsNotNull(FDAO.This.Id);
  FDAO.Delete;
end;

procedure TFormacaoERPTest.Setup;
begin
  FDAO := TDAONotaFiscal.New(FConnection);
end;

procedure TFormacaoERPTest.TearDown;
begin
end;

procedure TFormacaoERPTest.TestInsertSemCNPJ;
begin
  FDAO.This
    .CNPJEmitente('');

  Assert.WillRaiseWithMessage(
    procedure
    begin
      FDAO.Insert;
    end,
    Exception,
    'Erro ao inserir nota fiscal: CNPJ Emitente é obrigatório.');
end;

procedure TFormacaoERPTest.Update;
var
  chave: string;
begin
  FDAO.Get('Numero', '1');
  chave := FDAO.This.ChaveEletronica;

  FDAO.This.ChaveEletronica('12345');

  FDAO.Update;

  FDAO.Get('Numero', '1');
  Assert.AreEqual('12345', FDAO.This.ChaveEletronica);

  FDAO.This.ChaveEletronica(chave);
  FDAO.Update;
end;

initialization
  TDUnitX.RegisterTestFixture(TFormacaoERPTest);

end.
